buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
        springDependencyManagementVersion = '1.0.6.RELEASE'
        gitVersion = '0.11.0'
        dockerPluginVersion = '3.5.0'
    }
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("com.bmuschko:gradle-docker-plugin:${dockerPluginVersion}")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.palantir.gradle.gitversion:gradle-git-version:${gitVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:${springDependencyManagementVersion}")
        classpath("com.aries:docker-java-shaded:3.1.0-rc-3")
    }
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'com.bmuschko.docker-spring-boot-application'
apply plugin: 'com.palantir.git-version'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 8
targetCompatibility = 8

def versionDetails = versionDetails()
version "${versionDetails.lastTag}-${versionDetails.gitHash}"

repositories {
    jcenter()
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-data-rest"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.liquibase:liquibase-core"
    runtime "org.hibernate:hibernate-java8"


    runtime "org.springframework.boot:spring-boot-devtools"
    runtime "org.springframework.data:spring-data-rest-hal-browser"
    runtime "com.h2database:h2"
    runtime "org.postgresql:postgresql"

    testCompile "org.springframework.boot:spring-boot-starter-test"
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}
sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java.srcDir file('src/integration-test/java')
    }
    test {
        resources.srcDir file('src/integrationTest/resources')
    }
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    systemProperty "integrationTestBaseUrl", project.getProperty("integrationTestBaseUrl")

    testLogging {
        events "passed", "skipped", "failed"
    }
}

docker {
    url 'unix:///var/run/docker.sock'
    springBootApplication {
        baseImage = 'openjdk:8-alpine'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport