buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
        springDependencyManagementVersion = '1.0.6.RELEASE'
        gitVersion = '0.11.0'
        dockerPluginVersion = '3.4.4'
    }
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("com.bmuschko:gradle-docker-plugin:${dockerPluginVersion}")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.palantir.gradle.gitversion:gradle-git-version:${gitVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:${springDependencyManagementVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'com.bmuschko.docker-remote-api'
apply plugin: 'com.palantir.git-version'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.8

def versionDetails = versionDetails()
version "${versionDetails.lastTag}-${versionDetails.gitHash}"

repositories {
    mavenCentral()
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-data-rest"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.liquibase:liquibase-core"
    runtime "org.hibernate:hibernate-java8"


    runtime "org.springframework.boot:spring-boot-devtools"
    runtime "org.springframework.data:spring-data-rest-hal-browser"
    runtime "com.h2database:h2"
    runtime "org.postgresql:postgresql"

    testCompile "org.springframework.boot:spring-boot-starter-test"
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}
sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java.srcDir file('src/integration-test/java')
    }
    test {
        resources.srcDir file('src/integrationTest/resources')
    }
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    systemProperty "integrationTestBaseUrl", project.getProperty("integrationTestBaseUrl")

    testLogging {
        events "passed", "skipped", "failed"
    }
}

docker {
    url 'unix:///var/run/docker.sock'
}

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

task createDockerfile(type: Dockerfile) {
    destFile = project.file('build/Dockerfile')
    from 'java:8'
    maintainer 'Nicolas Byl "nicolas.byl@codecentric.de"'
    addFile 'libs/confy.jar', 'app.jar'
    exposePort 8080
    defaultCommand "java", "-Djava.security.egd=file:/dev/./urandom", "-jar", "/app.jar"
}

task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = createDockerfile.destFile.parentFile
    tag = "confy:${project.version}"
}

task publishImage() {
//task publishImage(type: DockerPushImage) {
    dependsOn buildImage
//    imageName = "${registryLocation}/${project.name}:${project.version}"
}
